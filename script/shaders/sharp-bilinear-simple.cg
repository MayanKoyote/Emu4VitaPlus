/*
   Author: rsn8887 (based on TheMaister)
   License: Public domain

   This is an integer prescale filter that should be combined
   with a bilinear hardware filtering (GL_BILINEAR filter or some such) to achieve
   a smooth scaling result with minimum blur. This is good for pixelgraphics
   that are scaled by non-integer factors.
   
   The prescale factor and texel coordinates are precalculated
   in the vertex shader for speed.
*/

struct input
{
    float2 video_size;
    float2 texture_size;
    float2 output_size;
    float frame_count;
    float frame_direction;
    float frame_rotation;
};

#if defined(VERTEX)
void main(
  float2 aTexcoord : TEXCOORD0,
  float4 aPosition : POSITION,
  uniform float4x4 wvp,
  uniform input IN,
  out float4 oPosition : POSITION,
  out float2 oTexCoord : TEXCOORD0,
  out float2 texel : TEXCOORD1,
  out float2 scale : TEXCOORD2)
{
    oPosition = mul(aPosition, wvp);
    oTexCoord = aTexcoord;
    texel = aTexcoord * float4(IN.texture_size, 1.0 / IN.texture_size).xy;
    scale = max(floor(float4(IN.output_size, 1.0 / IN.output_size).xy / IN.video_size.xy), float2(1.0, 1.0));
}
#elif defined(FRAGMENT)
float4 main(
  uniform sampler2D vTexture : TEXUNIT0,
  uniform input IN,
  float2 TexCoord : TEXCOORD0,
  float2 texel : TEXCOORD1,
  float2 scale : TEXCOORD2
) : COLOR
{
   float2 texel_floored = floor(texel);
   float2 s = frac(texel);
   float2 region_range = 0.5 - 0.5 / scale;

   // Figure out where in the texel to sample to get correct pre-scaled bilinear.
   // Uses the hardware bilinear interpolator to avoid having to sample 4 times manually.

   float2 center_dist = s - 0.5;
   float2 f = (center_dist - clamp(center_dist, -region_range, region_range)) * scale + 0.5;

   float2 mod_texel = texel_floored + f;

   return float4(tex2D(vTexture, mod_texel / float4(IN.texture_size, 1.0 / IN.texture_size).xy).rgb, 1.0);
}
#endif